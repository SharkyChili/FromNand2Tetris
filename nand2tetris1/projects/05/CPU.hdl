// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    //---------------------------------------------------------------------------------------------------
    Mux16(a = instruction, b = outAD,sel = instruction[15], out = inA);
    //第一个判断条件肯定是[15],因为如果是一个[5]位恰好为1的A指令，那么进入的是ALU的输出，显然不对
    //第二个条件，检查ARegister的情况，应该是为A指令时，都输入，为C指令且ALU输出到A时，输入，如图
    //             [15]         [5]         输入A
    //              0            0             1
    //              0            1             1
    //              1            0             0
    //              1            1             1
    //那么我们取   （非[15]）或[5] 即可达成
    Not(in = instruction[15],out = not15);
    Or(a = not15, b = instruction[5],out = ARegisterload);
    ARegister(in = inA ,load = ARegisterload ,out[0..14] = addressM,out = inMUXA);
    Mux16(a = inMUXA, b = inM, sel = instruction[12], out = inALUAM);
    //----------------------------------------------------------------------------------------------------    

    //仅有当为C指令，且[4] = 1时，才load
    And(a = instruction[15],b = instruction[4], out = loadD);
    DRegister(in = outAD ,load = loadD ,out = inALUD);
    ALU(x= inALUD,y= inALUAM,zx=instruction[11] ,nx=instruction[10] ,zy=instruction[9] ,ny=instruction[8],f=instruction[7] ,no=instruction[6] ,out = outAD,out=outM ,zr=zr ,ng=ng);
    //-------------------------------------------------------------------------------------------------------

    //只有当ng和zr都为0时，才大于0，  不是简单的notng
    Not(in = ng , out = notng);
    Not(in = zr , out = notzr);
    And(a = notng, b = notzr , out = postive);
    //然后三次and再or，也就是，指令开了几道门，随便从哪一道都可以
    And(a = instruction[2], b = ng, out = tmp1);
    And(a = instruction[1], b = zr, out = tmp2);
    And(a = instruction[0], b = postive, out = tmp3);
    Or(a = tmp1,b = tmp2, out = tmp4);
    Or(a = tmp4,b = tmp3, out = tmp5);
    //只有当为C指令时才跳
    And(a = tmp5,b = instruction[15],out = pcload);
    PC(in= inMUXA,load=pcload ,inc=true ,reset= reset,out[0..14]= pc);

    //--------------------------------------------------------------------------------------------------------
    And(a = instruction[15],b = instruction[3],out = writeM);
    
}